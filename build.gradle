plugins {
    id 'org.jetbrains.kotlin.jvm' version "1.7.10"
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.jlleitschuh.gradle.ktlint' version "10.3.0"
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.dropwizard:dropwizard-core:2.1.1'
    implementation 'io.dropwizard:dropwizard-client:2.1.1'
    implementation 'dev.failsafe:failsafe:3.2.4'
    implementation 'com.google.inject:guice:5.1.0'
    implementation 'ru.vyarus:dropwizard-guicey:5.6.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.7.10"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"

    testImplementation 'io.rest-assured:rest-assured:5.1.1'
    testImplementation 'io.rest-assured:kotlin-extensions:5.1.1'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"
    testImplementation "io.dropwizard:dropwizard-testing:2.1.1"
    testImplementation "com.github.tomakehurst:wiremock-jre8:2.33.2"
    testImplementation 'com.willowtreeapps.assertk:assertk-jvm:0.25'
    testImplementation "io.mockk:mockk:1.12.4"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

shadowJar {
    zip64 true
    mergeServiceFiles()
    archivesBaseName = "microservice"
}

mainClassName = 'MainKt'
run {
    args 'server', 'dev.yml'
}

sourceSets {
    integration {
        kotlin {
            srcDir 'src/integration/kotlin'
        }
        resources {
            srcDir 'src/integration/resource'
        }
        compileClasspath += sourceSets.main.runtimeClasspath
        compileClasspath += sourceSets.test.runtimeClasspath
    }
}

task integrationTest(type: Test) {
    description = "Runs Integration Tests"
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath += sourceSets.integration.runtimeClasspath
    useJUnitPlatform()
}
 test.finalizedBy(integrationTest)