plugins {
    id 'org.jetbrains.kotlin.jvm' version "1.9.10"
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.jlleitschuh.gradle.ktlint' version "11.5.1"
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

project.ext {
    kotlinVersion = '1.9.10'
    coroutinesVersion = '1.7.3'
    dropwizardVersion = '4.0.1'
    guiceVersion = '7.0.0'
    dropwizardGuiceyVersion = '7.0.1'
    junitVersion = '5.10.0'
    restAssuredVersion = '5.3.1'
    assertKVersion = '0.26.1'
    mockKVersion = '1.13.3'
    wiremockVersion = '3.0.0'
}

dependencies {
    // Runtime dependencies
    implementation "io.dropwizard:dropwizard-core:$dropwizardVersion"
    implementation "io.dropwizard:dropwizard-client:$dropwizardVersion"
    implementation "com.google.inject:guice:$guiceVersion"
    implementation "ru.vyarus:dropwizard-guicey:$dropwizardGuiceyVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"

    // Test dependencies
    testImplementation "io.rest-assured:rest-assured:$restAssuredVersion"
    testImplementation "io.rest-assured:kotlin-extensions:$restAssuredVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"
    testImplementation "io.dropwizard:dropwizard-testing:$dropwizardVersion"
    testImplementation "com.github.tomakehurst:wiremock-standalone:$wiremockVersion"
    testImplementation "com.willowtreeapps.assertk:assertk-jvm:$assertKVersion"
    testImplementation "io.mockk:mockk:$mockKVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

test {
    useJUnitPlatform()
}

shadowJar {
    zip64 true
    mergeServiceFiles()
    archivesBaseName = "microservice"
}

mainClassName = 'MainKt'

run {
    args 'server', 'config/dev.yml'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

sourceSets {
    integration {
        kotlin {
            srcDir 'src/integration/kotlin'
        }
        resources {
            srcDir 'src/integration/resource'
        }
        compileClasspath += sourceSets.main.runtimeClasspath
        compileClasspath += sourceSets.test.runtimeClasspath
    }
}

tasks.register('integrationTest', Test) {
    description = "Runs Integration Tests"
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath += sourceSets.integration.runtimeClasspath
    useJUnitPlatform()
}

build.dependsOn integrationTest